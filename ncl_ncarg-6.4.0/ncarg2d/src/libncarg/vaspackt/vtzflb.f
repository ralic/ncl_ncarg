      SUBROUTINE VTZFLB (IACT,RWRK,IAMA)
C
      DIMENSION RWRK(*),IAMA(*)
C
C VTZFLB generates the zero-field label.  If IACT = 1, the label is
C plotted.  If IACT = 2, the label box is added to the area map in IAMA.
C
C Declare all of the VASPACKT common blocks.
C
C
C VTCOM1 contains integer and real variables.
C
      COMMON /VTCOM1/ AHAW,AHLN,AHLR,AHSP,AHSR
      COMMON /VTCOM1/ ANIL,ANM1,ANM2,ANZF,AVEL,CHWM,CXIL,CXZF
      COMMON /VTCOM1/ CYIL,CYZF,DCNU,DCNV,DCNW,DMAX
      COMMON /VTCOM1/ DMIN,DVAL  !  REUSE FOR FLOWMIN AND FLOWMAX?
      COMMON /VTCOM1/ EMAX,EPSI
      COMMON /VTCOM1/ IBIL,IBZF,ICIL,ICLR(255)
      COMMON /VTCOM1/ ICSG,ICST,ICTT,ICTV,ICZF,IDBG,IISP
      COMMON /VTCOM1/ IIWS(2),IIWU,ILBC,IMPF
      COMMON /VTCOM1/ INIL  !  NEEDED? (INFORMATIONAL LABEL INDEX)
      COMMON /VTCOM1/ INIT,IPAI,IPIS
      COMMON /VTCOM1/ IPIL,IPZF,IRNG,IRWS(2),IRWU,ISET,ISTA(625)
      COMMON /VTCOM1/ ISVT,ITBM,IWSO,IZFF,JODP,JOMA
      COMMON /VTCOM1/ JOTZ,LCTM,LEA1,LEA2,LEA3,LEE1,LEE2,LEE3
      COMMON /VTCOM1/ LIWB,LIWK,LIWS(2),LNLG
      COMMON /VTCOM1/ LOEN,LOPN,LOTN
      COMMON /VTCOM1/ LRWK,LRWS(2)
      COMMON /VTCOM1/ LSDD,LSDL,LSDM,LTIL,LTZF,MIRO,NCLR
      COMMON /VTCOM1/ NDGL,NEDG,NEXL,NEXT,NEXU
      COMMON /VTCOM1/ NLBS  !  NEEDED? (NUMBER OF LABELS)
      COMMON /VTCOM1/ NLSD,NLZF,NOMF,NPNT
      COMMON /VTCOM1/ NR04  !  NEEDED? (LABEL-LIST MANAGEMENT)
      COMMON /VTCOM1/ NSDL,NSDR,NTRI,OORV,PCPX,PCPY,PCPZ
      COMMON /VTCOM1/ PITH  !  NEEDED? (STREAMLINE INTERPOLATION)
      COMMON /VTCOM1/ SCFS,SCFU  !  NEEDED? (SCALE FACTORS)
      COMMON /VTCOM1/ SLLN,SLLR,SLPS,SLPR,SLSP,SLSR,SVSP,SVSR
      COMMON /VTCOM1/ TTLL,TTLR,TTSP,TTSR,TVAL(0:256)
      COMMON /VTCOM1/ UVPB,UVPL,UVPR,UVPS,UVPT,UWDB,UWDL
      COMMON /VTCOM1/ UWDR,UWDT,VFRA,VRLN,VRLR,VRMG,VRMR
      COMMON /VTCOM1/ VVMM
      COMMON /VTCOM1/ WCIL,WCZF,WLIL,WLZF
      COMMON /VTCOM1/ WWIL,WWZF
      COMMON /VTCOM1/ XLBC,XMAX,XMIN,XVPL,XVPR,XWDL,XWDR
      COMMON /VTCOM1/ YLBC,YMAX,YMIN,YVPB,YVPT,YWDB,YWDT,ZMAX,ZMIN
C
      EQUIVALENCE (IIWS(1),II01),(LIWS(1),LI01)
      EQUIVALENCE (IIWS(2),II02),(LIWS(2),LI02)
      EQUIVALENCE (IRWS(1),IR01),(LRWS(1),LR01)
      EQUIVALENCE (IRWS(2),IR02),(LRWS(2),LR02)
      SAVE   /VTCOM1/
C
C VTCOM2 holds character parameters.
C
      COMMON /VTCOM2/ CHEX,CTMA,CTMB,FRMT
      COMMON /VTCOM2/ TXIL,TXZF
      CHARACTER*13 CHEX
      CHARACTER*500 CTMA,CTMB
      CHARACTER*8 FRMT
      CHARACTER*128 TXIL
      CHARACTER*64 TXZF
      SAVE   /VTCOM2/
C
C
C Declare local arrays to hold coordinates for area fill of boxes.
C
      DIMENSION BFXC(4),BFYC(4)
C
C Define some local arrays in which to retrieve information from GKS.
C
      DIMENSION DUMI(4),VPRT(4),WIND(4)
C
C Define some arithmetic statement functions to get from the fractional
C system to the world system.
C
      CFWX(X)=WIND(1)+(WIND(2)-WIND(1))*(X-VPRT(1))/(VPRT(2)-VPRT(1))
      CFWY(Y)=WIND(3)+(WIND(4)-WIND(3))*(Y-VPRT(3))/(VPRT(4)-VPRT(3))
C
C Retrieve the definitions of the current GKS window and viewport.
C
      CALL GQCNTN (IGER,NCNT)
C
      IF (IGER.NE.0) THEN
        CALL SETER ('VTZFLB - ERROR EXIT FROM GQCNTN',1,1)
        RETURN
      END IF
C
      CALL GQNT (NCNT,IGER,WIND,VPRT)
C
      IF (IGER.NE.0) THEN
        CALL SETER ('VTZFLB - ERROR EXIT FROM GQNT',2,1)
        RETURN
      END IF
C
C If the text string for the zero-field label is blank, do nothing.
C
      IF (TXZF(1:LTZF).EQ.' ') RETURN
C
C Otherwise, form the zero-field label ...
C
      DVAL=DMIN
      CALL VTSBST (TXZF(1:LTZF),CTMA,LCTM)
C
C ... get sizing information for the label ...
C
      XPFS=XVPL+CXZF*(XVPR-XVPL)
      YPFS=YVPB+CYZF*(YVPT-YVPB)
      XLBC=CFUX(XPFS)
      IF (ICFELL('VTZFLB',3).NE.0) RETURN
      YLBC=CFUY(YPFS)
      IF (ICFELL('VTZFLB',4).NE.0) RETURN
      WCFS=CHWM*WCZF*(XVPR-XVPL)
      WWFS=CHWM*WWZF*(XVPR-XVPL)
C
      CALL PCGETI ('TE',ISTE)
      IF (ICFELL('VTZFLB',5).NE.0) RETURN
      CALL PCSETI ('TE',1)
      IF (ICFELL('VTZFLB',6).NE.0) RETURN
      CALL HLUVTCHZF (+1)
      IF (ICFELL('VTZFLB',7).NE.0) RETURN
      CALL PLCHHQ (XLBC,YLBC,CTMA(1:LCTM),WCFS,360.,0.)
      IF (ICFELL('VTZFLB',8).NE.0) RETURN
      CALL HLUVTCHZF (-1)
      IF (ICFELL('VTZFLB',9).NE.0) RETURN
      CALL PCGETR ('DL',DSTL)
      IF (ICFELL('VTZFLB',10).NE.0) RETURN
      CALL PCGETR ('DR',DSTR)
      IF (ICFELL('VTZFLB',11).NE.0) RETURN
      CALL PCGETR ('DB',DSTB)
      IF (ICFELL('VTZFLB',12).NE.0) RETURN
      CALL PCGETR ('DT',DSTT)
      IF (ICFELL('VTZFLB',13).NE.0) RETURN
      CALL PCSETI ('TE',ISTE)
      IF (ICFELL('VTZFLB',14).NE.0) RETURN
      DSTL=DSTL+WWFS
      DSTR=DSTR+WWFS
      DSTB=DSTB+WWFS
      DSTT=DSTT+WWFS
C
C ... and then take the desired action, either plotting the label or
C putting a box around it into the area map.
C
      SINA=SIN(.017453292519943*ANZF)
      COSA=COS(.017453292519943*ANZF)
C
      IXPO=MOD(IPZF+4,3)-1
C
      IF (IXPO.LT.0) THEN
        XPFS=XPFS+DSTL*COSA
        YPFS=YPFS+DSTL*SINA
      ELSE IF (IXPO.GT.0) THEN
        XPFS=XPFS-DSTR*COSA
        YPFS=YPFS-DSTR*SINA
      END IF
C
      IYPO=(IPZF+4)/3-1
C
      IF (IYPO.LT.0) THEN
        XPFS=XPFS-DSTB*SINA
        YPFS=YPFS+DSTB*COSA
      ELSE IF (IYPO.GT.0) THEN
        XPFS=XPFS+DSTT*SINA
        YPFS=YPFS-DSTT*COSA
      END IF
C
      XLBC=CFUX(XPFS)
      IF (ICFELL('VTZFLB',15).NE.0) RETURN
      YLBC=CFUY(YPFS)
      IF (ICFELL('VTZFLB',16).NE.0) RETURN
C
      IF (IACT.EQ.1) THEN
        IF (MOD(IBZF/2,2).NE.0) THEN
          JLBC=ILBC
          IF (JLBC.GE.0) THEN
            CALL GQFACI (IGER,ISFC)
            IF (IGER.NE.0) THEN
              CALL SETER ('VTZFLB - ERROR EXIT FROM GQFACI',17,1)
              RETURN
            END IF
            IF (ISFC.NE.JLBC) CALL GSFACI (JLBC)
          END IF
          CALL HLUVTCHZF (+2)
          IF (ICFELL('VTZFLB',18).NE.0) RETURN
          BFXC(1)=CFWX(XPFS-DSTL*COSA+DSTB*SINA)
          IF (ICFELL('VTZFLB',19).NE.0) RETURN
          BFYC(1)=CFWY(YPFS-DSTL*SINA-DSTB*COSA)
          IF (ICFELL('VTZFLB',20).NE.0) RETURN
          BFXC(2)=CFWX(XPFS+DSTR*COSA+DSTB*SINA)
          IF (ICFELL('VTZFLB',21).NE.0) RETURN
          BFYC(2)=CFWY(YPFS+DSTR*SINA-DSTB*COSA)
          IF (ICFELL('VTZFLB',22).NE.0) RETURN
          BFXC(3)=CFWX(XPFS+DSTR*COSA-DSTT*SINA)
          IF (ICFELL('VTZFLB',23).NE.0) RETURN
          BFYC(3)=CFWY(YPFS+DSTR*SINA+DSTT*COSA)
          IF (ICFELL('VTZFLB',24).NE.0) RETURN
          BFXC(4)=CFWX(XPFS-DSTL*COSA-DSTT*SINA)
          IF (ICFELL('VTZFLB',25).NE.0) RETURN
          BFYC(4)=CFWY(YPFS-DSTL*SINA+DSTT*COSA)
          IF (ICFELL('VTZFLB',26).NE.0) RETURN
          CALL GFA (4,BFXC,BFYC)
          CALL HLUVTCHZF (-2)
          IF (ICFELL('VTZFLB',27).NE.0) RETURN
          IF (JLBC.GE.0) THEN
            IF (ISFC.NE.JLBC) CALL GSFACI (ISFC)
          END IF
        END IF
        CALL GQPLCI (IGER,ISLC)
        IF (IGER.NE.0) THEN
          CALL SETER ('VTZFLB - ERROR EXIT FROM GQPLCI',28,1)
          RETURN
        END IF
        CALL GQTXCI (IGER,ISTC)
        IF (IGER.NE.0) THEN
          CALL SETER ('VTZFLB - ERROR EXIT FROM GQTXCI',29,1)
          RETURN
        END IF
        IF (ICZF.GE.0) THEN
          JCZF=ICZF
        ELSE
          JCZF=ISTC
        END IF
        JSLC=ISLC
        JSTC=ISTC
        IF (JSLC.NE.JCZF) THEN
          CALL PLOTIF (0.,0.,2)
          IF (ICFELL('VTZFLB',30).NE.0) RETURN
          CALL GSPLCI (JCZF)
          JSLC=JCZF
        END IF
        IF (JSTC.NE.JCZF) THEN
          CALL GSTXCI (JCZF)
          JSTC=JCZF
        END IF
        CALL GQCLIP (IGER,IGCF,DUMI)
        IF (IGER.NE.0) THEN
          CALL SETER ('VTZFLB - ERROR EXIT FROM GQCLIP',31,1)
          RETURN
        END IF
        IF (IGCF.NE.0) THEN
          CALL PLOTIF (0.,0.,2)
          IF (ICFELL('VTZFLB',32).NE.0) RETURN
          CALL GSCLIP (0)
        END IF
        CALL HLUVTCHZF (+3)
        IF (ICFELL('VTZFLB',33).NE.0) RETURN
        CALL PLCHHQ (XLBC,YLBC,CTMA(1:LCTM),WCFS,ANZF,0.)
        IF (ICFELL('VTZFLB',34).NE.0) RETURN
        CALL HLUVTCHZF (-3)
        IF (ICFELL('VTZFLB',35).NE.0) RETURN
        IF (IGCF.NE.0) THEN
          CALL PLOTIF (0.,0.,2)
          IF (ICFELL('VTZFLB',36).NE.0) RETURN
          CALL GSCLIP (IGCF)
        END IF
        IF (MOD(IBZF,2).NE.0) THEN
          WDTH=WLZF
          IF (WDTH.GT.0.) THEN
            CALL GQLWSC (IGER,SFLW)
            IF (IGER.NE.0) THEN
              CALL SETER ('VTZFLB - ERROR EXIT FROM GQLWSC',37,1)
              RETURN
            END IF
            CALL PLOTIF (0.,0.,2)
            IF (ICFELL('VTZFLB',38).NE.0) RETURN
            CALL GSLWSC (WDTH)
          END IF
          CALL HLUVTCHZF (+4)
          IF (ICFELL('VTZFLB',39).NE.0) RETURN
          CALL PLOTIF (XPFS-DSTL*COSA+DSTB*SINA,
     +                 YPFS-DSTL*SINA-DSTB*COSA,0)
          IF (ICFELL('VTZFLB',40).NE.0) RETURN
          CALL PLOTIF (XPFS+DSTR*COSA+DSTB*SINA,
     +                 YPFS+DSTR*SINA-DSTB*COSA,1)
          IF (ICFELL('VTZFLB',41).NE.0) RETURN
          CALL PLOTIF (XPFS+DSTR*COSA-DSTT*SINA,
     +                 YPFS+DSTR*SINA+DSTT*COSA,1)
          IF (ICFELL('VTZFLB',42).NE.0) RETURN
          CALL PLOTIF (XPFS-DSTL*COSA-DSTT*SINA,
     +                 YPFS-DSTL*SINA+DSTT*COSA,1)
          IF (ICFELL('VTZFLB',43).NE.0) RETURN
          CALL PLOTIF (XPFS-DSTL*COSA+DSTB*SINA,
     +                 YPFS-DSTL*SINA-DSTB*COSA,1)
          IF (ICFELL('VTZFLB',44).NE.0) RETURN
          CALL PLOTIF (0.,0.,2)
          IF (ICFELL('VTZFLB',45).NE.0) RETURN
          CALL HLUVTCHZF (-4)
          IF (ICFELL('VTZFLB',46).NE.0) RETURN
          IF (WDTH.GT.0.) THEN
            CALL PLOTIF (0.,0.,2)
            IF (ICFELL('VTZFLB',47).NE.0) RETURN
            CALL GSLWSC (SFLW)
          END IF
        END IF
        IF (ISLC.NE.JSLC) THEN
          CALL PLOTIF (0.,0.,2)
          IF (ICFELL('VTZFLB',48).NE.0) RETURN
          CALL GSPLCI (ISLC)
        END IF
        IF (ISTC.NE.JSTC) CALL GSTXCI (ISTC)
      ELSE
        CALL VTGRWS (RWRK,1,10,IWSE)
        IF (IWSE.NE.0.OR.ICFELL('VTZFLB',49).NE.0) RETURN
        ANLB=.017453292519943*ANZF
        SALB=SIN(ANLB)
        CALB=COS(ANLB)
        RWRK(IR01+ 1)=CFUX(XPFS-DSTL*CALB+DSTB*SALB)
        IF (ICFELL('VTZFLB',50).NE.0) RETURN
        RWRK(IR01+ 2)=CFUX(XPFS+DSTR*CALB+DSTB*SALB)
        IF (ICFELL('VTZFLB',51).NE.0) RETURN
        RWRK(IR01+ 3)=CFUX(XPFS+DSTR*CALB-DSTT*SALB)
        IF (ICFELL('VTZFLB',52).NE.0) RETURN
        RWRK(IR01+ 4)=CFUX(XPFS-DSTL*CALB-DSTT*SALB)
        IF (ICFELL('VTZFLB',53).NE.0) RETURN
        RWRK(IR01+ 5)=RWRK(IR01+1)
        RWRK(IR01+ 6)=CFUY(YPFS-DSTL*SALB-DSTB*CALB)
        IF (ICFELL('VTZFLB',54).NE.0) RETURN
        RWRK(IR01+ 7)=CFUY(YPFS+DSTR*SALB-DSTB*CALB)
        IF (ICFELL('VTZFLB',55).NE.0) RETURN
        RWRK(IR01+ 8)=CFUY(YPFS+DSTR*SALB+DSTT*CALB)
        IF (ICFELL('VTZFLB',56).NE.0) RETURN
        RWRK(IR01+ 9)=CFUY(YPFS-DSTL*SALB+DSTT*CALB)
        IF (ICFELL('VTZFLB',57).NE.0) RETURN
        RWRK(IR01+10)=RWRK(IR01+6)
        IF ((XWDL.LT.XWDR.AND.YWDB.LT.YWDT).OR.(XWDL.GT.XWDR.AND.YWDB.GT
     +.YWDT)) THEN
          CALL AREDAM (IAMA,RWRK(IR01+1),RWRK(IR01+6),5,IGLB,-1,0)
          IF (ICFELL('VTZFLB',58).NE.0) RETURN
        ELSE
          CALL AREDAM (IAMA,RWRK(IR01+1),RWRK(IR01+6),5,IGLB,0,-1)
          IF (ICFELL('VTZFLB',59).NE.0) RETURN
        END IF
        LR01=0
      END IF
C
C Done.
C
      RETURN
C
      END
